
plugins {
    id 'org.asciidoctor.convert' version '1.5.6' apply false 
    id 'eclipse'
}

// Include a gradle script that has all of our dependencies split out.
apply from: "gradle/dependencies.gradle"

allprojects {
    repositories {
        jcenter() 
    }
}

subprojects {
    version = '1.0'
}

configure( subprojects.findAll { it.name == 'greeter' ||
				it.name == 'greeting-library' ||
				it.name == 'common' ||
				it.name == 'undertow' ||
				it.name == 'examples' ||
				it.name == 'undertow-routing' ||
				it.name == 'undertow-hello-world'
				} ) {

    // groovy includes java
    apply plugin: 'groovy'

    dependencies {
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = [
                	"src/main/java",
                	"src/generated/java"]
            }
            resources {
                srcDirs = [
                	"src/main/resources",
                	"ui/assets"]
            }
        }
    }

    configurations.all {

        resolutionStrategy {

            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            // Auto force all of our explicit dependencies.
            libs.each { k, v -> force(v) }
            force('commons-logging:commons-logging:1.2')
            force('com.google.code.findbugs:jsr305:3.0.2')

            // cache dynamic versions for 10 minutes
            cacheDynamicVersionsFor 10*60, 'seconds'
            // don't cache changing modules at all
            cacheChangingModulesFor 0, 'seconds'
        }
    }

}
